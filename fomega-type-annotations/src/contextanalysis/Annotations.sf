module contextanalysis.Annotations

rules

  add-context-errors(|msgs) =
      if <?[]> msgs
      then id
      else 
         where(!(msgs, <id>); debug-analysis(!"ADD-CTX-ERRS "))
       ; where(get-context-errors; !(msgs, <id>); conc; nub => all-msgs)
       ; put-analysis-data(|"context-errors", all-msgs)
      end
  
  add-fresh-context-errors(|msgs) =
      if oncetd(get-context-errors; not(?[]))
        then id
        else add-context-errors(|msgs)
      end

  get-context-errors = get-analysis-data(|"context-errors") <+ ![]

  collect-all-context-errors = collect-all(get-context-errors); concat

  context-errors-as-alist =
    let as-alist =
      { node, errs:
          ?node
        ; <get-context-errors> node => errs
        ; if !errs; ?[] then fail else ![(node, errs)] end
      }
    in collect-all(as-alist) end

  get-kind = get-analysis-data(|"kind")

  put-kind = ?(K, node); <put-analysis-data(|"kind", K)> node

  get-signature = get-analysis-data(|"signature")

  put-signature = ?(sig, node); <put-analysis-data(|"signature", sig)> node

  get-type = get-analysis-data(|"type")

  put-type = ?(T, node); <put-analysis-data(|"type", T)> node

  get-context = get-analysis-data(|"context")

  put-context = ?(C, node); <put-analysis-data(|"context", C)> node
  
  mark-context-normalized = put-analysis-data(|"context-normalized", ())
  is-context-normalized = where(get-analysis-data(|"context-normalized"))
