module contextanalysis.RuleDesugaring

import org.sugarj.languages.Fomega
import org.sugarj.languages.Stratego

import concretesyntax.Metavar
import concretesyntax.Stratego

import contextanalysis.RuleSyntax


desugarings
  desugar-type-rule


rules

  desugar-type-rule :
    TypeRule(premises, RuleName(sep, optional-name), conclusion) ->
      transformation-elem(<translate-type-rule> (optional-name, premises, conclusion))


rules // Translate a type rule

  translate-type-rule :
    (Some(Var(name)), premises, TypingJudgment(C, e, T)) ->
    stratego-decl
    |[ rules
         annotate-type-defined =
             ?(term~tC, term~te)
           ; strategy~premises-implementation
    ]|
    where <trm-explode> C => tC
        ; <trm-explode> e => te
        ; <translate-subst> T => tT
        ; <premises-to-list> premises => premises-list
        ; <translate-premises> (<ts-initial> (), premises-list) => (ts1, premises-implementation)


rules // Code for all kind of rules

  premises-to-list :
    NoPremises -> []
  premises-to-list :
    PremiseList(premises) -> <premises-to-list> premises
  premises-to-list :
    PremiseCons(judgment, premises) ->
      [judgment | <premises-to-list> premises]
  premises-to-list :
    judgment -> [judgment]
    where if (!judgment; ?PremiseCons(_,_)) then fail else id end

  translate-premises :
    (ts, []) -> (ts, |[ id ]|)

  translate-premises :
    (ts, [judgment | judgments]) ->
      (ts2, |[ strategy~judgment-implementation ; strategy~judgments-implementation ]|)
    where <translate-judgment> (ts, judgment)   => (ts1, judgment-implementation)
        ; <translate-premises> (ts1, judgments) => (ts2, judgments-implementation)


rules // Translate a judgment

  translate-judgment :
    (ts, TypingJudgment(C, e, T)) ->
      (ts3, |[   <annotate-type> (term~tC, term~te)
               ; <get-type> term~te => term~result-var
               ; strategy~inspect-result
            ]|)
    where <trm-explode> C => tC
        ; <trm-explode> e => te
        ; <fresh-result-var> ts => (ts1, result-var)
        ; <ts-add-blame> (ts1, T, e) => ts2
        ; <translate-type-expectation> (ts2, e, result-var, [], T, []) => (ts3, inspect-result)

  translate-judgment :
    (ts, KindingJudgment(C, T, K)) ->
      (ts3, |[   <annotate-kind> (term~tC, term~tT)
               ; <get-type> term~tT => term~result-var
               ; strategy~inspect-result
            ]|)
    where <trm-explode> C => tC
        ; <trm-explode> T => tT
        ; <fresh-result-var> ts => (ts1, result-var)
        ; <ts-add-blame> (ts1, K, T) => ts2
        ; <translate-kind-expectation> (ts2, T, result-var, K) => (ts3, inspect-result)

  translate-judgment :
    (ts, NormalizationJudgment(C, T, T')) ->
      (ts3, |[   <norm> (term~tC, term~tT) => term~result-var
               ; strategy~inspect-result
            ]|)
    where <trm-explode> C => tC
        ; <trm-explode> T => tT
        ; <fresh-result-var> ts => (ts1, result-var)
        ; <ts-get-blame> (ts1, T) => blame
        ; <ts-add-blame> (ts1, T', blame) => ts2
        ; <translate-type-expectation> (ts2, blame, result-var, [], T', []) => (ts3, inspect-result)

  translate-judgment :
    (ts, VarInContextJudgment(x, T, C)) ->
      (ts4, |[   <lookup-expr-var> (term~tx, term~tC) => (term~result-var, term~errs-var)
               ; <add-context-errs> (term~tx, term~errs-var)
               ; strategy~inspect-result
            ]|)
    where <trm-explode> C => tC
        ; <trm-explode> x => tx
        ; <fresh-result-var> ts => (ts1, result-var)
        ; <fresh-errs-var> ts1 => (ts2, errs-var)
        ; <ts-add-blame> (ts2, T, x) => ts3
        ; <translate-type-expectation> (ts3, x, result-var, [], T, []) => (ts4, inspect-result)

   translate-judgment :
     (ts, QVarInContextJudgment(qx, T, C)) -> <translate-judgment> (ts, VarInContextJudgment(qx, T, C))

  // TODO: missing NotinTagJudgment, NotinSelJudgment etc.


rules // Translate a type expectation

  translate-type-expectation :
    (ts, blame, result-var, fv1, Metavar(mvar), fv2) ->
      ( ts3
      , |[   strategy~subst-fvs1
           ; strategy~subst-fvs2
           ; <typeEq> (term~result-var2, term~result-var1) => term~msgs-var
           ; <add-context-errors> (term~msgs-var, term~tblame)
        ]|)
    where <ts-in-eqs> (ts, mvar)
        ; <fresh-msgs-var> ts => (ts1, msgs-var)
        ; <trm-explode> blame => tblame
        ; <translate-fv-subst> (ts1, result-var, [result-var, mvar], fv1) => (ts2, subst-fvs1, result-var1)
        ; <translate-fv-subst> (ts2, mvar, [result-var, mvar], fv2) => (ts3, subst-fvs2, result-var2)

  translate-type-expectation :
    (ts, blame result-var, fv1, Metavar(mvar), fv2) ->
      (<ts-append-eq> (ts, mvar), |[ !term~result-var; ?term~mvar ]|)
    where <not(ts-in-eqs)> (ts, mvar)

  translate-type-expectation :
    (ts, result-var, fv1, TyBool(), fv2) ->
      (ts3, |[   <typeEqBool> term~result-var => term~msgs-var
               ; <msgsToErrsDummy> term~msgs-var => term~errs-var
            ]|)
    where <fresh-msgs-var> ts => (ts1, msgs-var)
        ; <fresh-errs-var> ts1 => (ts2, errs-var)
        ; <ts-append-errs-var> (ts2, errs-var) => ts3

rules // Translate a substitution operator in a type

  translate-subst :
    Subst(X, S, T) ->
      stratego-term |[ <subst> (X, term~(<translate-subst> S), term~(<translate-subst> T)) ]|

  translate-subst :
    TyArrow(T1, T2) -> stratego-term |[ TyArrow(term~(<translate-subst> T1), term~(<translate-subst> T2)) ]|

  translate-subst :
    TyForall(X, K1, T2) -> stratego-term |[ TyForall(term~tX, term~tK1, term~(<translate-subst> T2)) ]|
    where <trm-explode> X => tX
        ; <trm-explode> K1 => tK1

  translate-subst :
    Metavar(mvar) -> mvar

  translate-subst :
    TyVar(X) -> stratego-term |[ TyVar(tX) ]|
    where <trm-explode> X => tX

  translate-subst :
    TyBool() -> stratego-term |[ TyBool() ]|

  translate-subst :
    TyNat() -> stratego-term |[ TyNat() ]|

  translate-subst :
    TyString() -> stratego-term |[ TyString() ]|

  translate-subst :
    TyRecord(decls) -> stratego-term |[ TyRecord(term~(<translate-subst> decls)) ]|

  translate-subst :
    TyVariant(decls) -> stratego-term |[ TyVariant(term~(<translate-subst> decls)) ]|

  translate-subst :
    TyDeclsNil() -> stratego-term |[ TyDeclsNil() ]|

  translate-subst :
    TyDeclsCons(TyDecl(x, T), decls) ->
    stratego-term |[
      TyDeclsCons(TyDecl(term~(<trm-explode> x), term~(<translate-subst> T)), term~(<translate-subst> decls))
    ]| 


rules

  translate-bound-variable :
    (ts, result-var, Metavar(mvar)) -> stratego-strategy |[ !term~result-var; ?term~mvar ]|
    where <not(ts-in-eqs)> (ts, mvar)

  translate-bound-variable :
    (ts, result-var, Metavar(mvar)) -> |[ id ]|
    where <ts-in-eqs> (ts, mvar)

  translate-bound-variable :
    (ts, x) -> stratego-strategy |[ !"fail foo" ]|


rules // Generate code to handle free variables in type expressions

  translate-fv-subst :
    (ts, T, Ts, []) -> (ts, |[ id ]|, T)

  translate-fv-subst :
    (ts, T, Ts, [X|Xs]) ->
      ( ts3
      , |[   <fresh> (term~X, term~tTs) => term~result-var1
           ; <subst> (term~X, TyVar(term~result-var1), term~T) => term~result-var2
           ; strategy~subst-rest
        ]|
      , result-var)
    where <foldr( ! stratego-term |[ [] ]|
                , \ (hd,tl) -> |[ [term~hd | term~tl] ]| \)> Ts=> tTs
        ; <trm-explode> X => tX
        ; <fresh-result-var> ts => (ts1, result-var1)
        ; <fresh-result-var> ts1 => (ts2, result-var2)
        ; <translate-fv-subst> (ts2, result-var2, [TyVar(result-var1) | Ts], Xs) =>
            (ts3, subst-rest, result-var)


rules // Fresh variables

  fresh-var(|prefix) :
    ts -> ( <ts-inc-counter> ts
          , Var(<concat-strings> [prefix, "_", <write-to-string> (<ts-get-counter> ts)]))

  fresh-result-var = fresh-var(|"result")

  fresh-errs-var = fresh-var(|"errs")

  fresh-msgs-var = fresh-var(|"msgs")


rules // Translation state handling

  ts-initial :
    () -> (0, [], [], [])

  ts-get-counter :
    (n, eqs, errs, blame) -> n

  ts-set-counter :
    ((n, eqs, errs, blame), m) -> (m, eqs, errs, blame)

  ts-inc-counter :
    (n, eqs, errs, blame) -> (<add> (n, 1), eqs, errs, blame)

  ts-get-errs :
    (n, eqs, errs, blame) -> errs

  ts-set-errs :
    ((n, eqs, errs, blame), errs-new) -> (n, eqs, errs-new, blame)

  ts-append-errs-var :
    (ts, errs-var) -> ts1
    where <ts-get-errs> ts => errs-old
        ; <ts-set-errs> (ts, <concat> [errs-old, [errs-var]]) => ts1

  ts-get-eqs :
    (n, eqs, errs, blame) -> eqs

  ts-set-eqs :
    ((n, eqs, errs, blame), eqs-new) -> (n, eqs-new, errs, blame)

  ts-append-eq :
    (ts, var) -> ts1
    where <ts-get-eqs> ts => eqs-old
        ; <ts-set-eqs> (ts, <concat> [eqs-old, [var]]) => ts1

  ts-in-eqs = ?(ts, var); where(<ts-get-eqs> ts => eqs); <elem(structurally-equal)> (var, eqs)

  ts-get-blame :
    (n, eqs, errs, blame) -> blame

  ts-set-blame :
    ((n, eqs, errs, blame), blame-new) -> (n, eqs, errs, blame-new)

  ts-add-blame :
    (ts, prop, node) -> <ts-set-blame> (ts, [(prop, node) | blame-old])
    where <ts-get-blame> ts => blame-old

  ts-lookup-blame :
    (ts, prop) -> <lookup> (prop, <ts-get-blame> ts)
