module extension.Declaration

import contextanalysis.rules.Syntax
import contextanalysis.rules.Desugaring
import contextanalysis.rules.Verification
import contextanalysis.ContextAnalysis

context-free syntax
  "syntax" "{" Grammar* "}" -> ToplevelDeclaration {cons("SyntaxExtension")}
  "desugaring" StrategoId "{" StrategoDef* StrategoDecl* "}" -> ToplevelDeclaration {cons("DesugaringExtension")}
  "typing" "{" InferenceRule* "}" -> ToplevelDeclaration {cons("TypingExtension")}

rules
  constraint-error = check-typing-rule-subject
  
  check-typing-rule-subject :
    TypingRule(_, _, TypingJudgment(C, e, T)) ->
    [(e, "Subject does not match desugaring rule.")]
  where <not(desugar-extension-internal)> e
  
  // is generated from DesugaringExtension
  desugar-extension-internal = fail
         
rules
  desugar-extension :
    SyntaxExtension(grammars) ->
    SugarBody(body)
  where
      <map(!grammar-elem(<id>))> grammars => body 
    ; rules( Extension-Syntax := body )
  
  desugar-extension :
    DesugaringExtension(desugar-strat, trafos, decls) ->
    SugarBody(<conc> (grammars, body))
  where
      (Extension-Syntax <+ ![]) => grammars 
    ; !transformation-elem(Desugarings([PureDesugaring(CallNoArgs(SVar(desugar-strat)))])) => desugaring
    ; !transformation-elem(Rules([SDefNoArgs("desugar-extension-internal", CallNoArgs(SVar(desugar-strat)))])) => rinternal
    ; ![rinternal, transformation-elem(Rules(trafos)) | <map(!transformation-elem(<id>))> decls] => body
    ; rules( Extension-Trafos := [body] )
  
  desugar-extension :
    TypingExtension(checks) ->
    SugarBody(<conc> (grammars, trafos, checks))
  where
      (Extension-Syntax <+ ![]) => grammars 
    ; (Extension-Trafos <+ ![]) => trafos
//    ; rules( Extension-Checks := checks )
  
desugarings
  desugar-extension

