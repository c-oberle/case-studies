// TODO: the SDF2 file does not contain anything
// and the signature line in the Stratego file is missing
module typing.extensions.Let

import org.sugarj.languages.Fomega

import typing.rules.Syntax
import typing.rules.Desugaring


context-free syntax
  "let" FomegaID ":" FomegaType "=" FomegaExpr "in" FomegaExpr -> FomegaExpr {cons("Let")}

context-free priorities
  FomegaExpr FomegaExpr -> FomegaExpr >
  "let" FomegaID ":" FomegaType "=" FomegaExpr "in" FomegaExpr -> FomegaExpr

lexical syntax
  "let" -> Keyword
  "in"  -> Keyword

lexical restrictions
  "let" "in" -/- [A-Za-z0-9]


~C |- ~e1 : ~T1
~C |- ~S :: *
~C |- ~S ~> ~U
~C |- ~T1 ~> ~U
(~C;~%x:~U) |- ~e2 : ~T2
======================================= T-Let
~C |- (let ~%x : ~S = ~e1 in ~e2) : ~T2


desugarings
  desugar-let

rules
  desugar-let :
    Let(x, T, e1, e2) -> App(Abs(x, T, e2), e1)
