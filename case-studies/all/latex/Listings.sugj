package latex;

import editor.Editor;

import latex.Core;
import latex.CoreLib;
import editor.Origin;

import util.Strategies;

public sugar Listings {
  signature constructors CodeListing : code -> SugarJCode
  
  context-free syntax
    ToplevelDeclaration* -> SugarJCode {ast("Locked(CodeListing(<1>))")}
    SugarDefElem+ -> SugarJCode {ast("Locked(CodeListing(<1>))")}
    JavaBlockStm+ -> SugarJCode {ast("Locked(CodeListing(<1>))")}
    JavaExpr -> SugarJCode {ast("Locked(CodeListing(<1>))")}
    EditorServiceSection+ -> SugarJCode {ast("Locked(CodeListing(<1>))")}
    EditorServiceCompletionRule+ -> SugarJCode {ast("Locked(CodeListing(<1>))")}
    StrategoRuleDef+ -> SugarJCode {ast("Locked(CodeListing(<1>))"), avoid}
  
    "..." -> StrategoTerm {cons("AbreviativeTerm")}
  
  syntax 
    LatexDocument -> LatexCode {ast("Locked(CodeListing(<1>))")}
  
  syntax
    "|>" LatexTextContent "<|" -> <JavaId-CF> {cons("LatexJavaId")}
  
  lexical syntax
    ~[\!\n]* -> LstInlineExclContent
  
  syntax
    "\\sugarjinline" "!" <LAYOUT?-CF> <SugarJCode-CF> <LAYOUT?-CF> "!" -> InTextCommand {cons("SugarJInlineCommandExclamation")}
    "\\sugarjinline" "{" <LAYOUT?-CF> <SugarJCode-CF> <LAYOUT?-CF> "}" -> InTextCommand {cons("SugarJInlineCommandBrace")}
    "\\latexinline" "!" LatexCode "!" -> InTextCommand {cons("LatexInlineCommandExclamation")}
    "\\latexinline" "{" LatexCode "}" -> InTextCommand {cons("LatexInlineCommandBrace")}
    "\\lstinline" "!" <LstInlineExclContent-LEX> "!" -> InTextCommand {cons("LstInlineCommandExclamation")}
    "\\lstinline" "{" <PlainContent-LEX> "}" -> InTextCommand {cons("LstInlineCommandBrace")}
  
  lexical syntax
    "lstlisting" -> LatexLstlistingEnvironmentName
    "lstlisting" -> LatexTextEnvironmentName {reject}
    "lstsugarj" -> LatexLstsugarjEnvironmentName
    "lstsugarj" -> LatexTextEnvironmentName {reject}
  
  context-free syntax
    "style" "=" ListingStyle -> ListingOption {cons("ListingStyleOption")}
    "xleftmargin" "=" LatexDimension -> ListingOption {cons("ListingXLeftMargin")}
    "figure" -> ListingStyle {cons("ListingStyleFigure")}
    "[" {ListingOption ","}* "]" -> ListingOptions
  
  syntax
    "\\begin" "{" <LatexLstlistingEnvironmentName-LEX> "}"
    <PlainContent-LEX>
    "\\end" "{" <LatexLstlistingEnvironmentName-LEX> "}"
    -> InTextEnvironment {cons("LstlistingEnvironment")}

    "\\begin" "{" <LatexLstsugarjEnvironmentName-LEX> "}"
    <LAYOUT?-CF> <ListingOptions?-CF>
    <LAYOUT?-CF> <SugarJCode-CF> <LAYOUT?-CF>
    "\\end" "{" <LatexLstsugarjEnvironmentName-LEX> "}"
    -> InTextEnvironment {cons("LstsugarjEnvironment")}
    
  rules
    pp-listing-options : None -> ""
    pp-listing-options : Some(opts) -> <conc-strings> (opts-string, sep)
      where <map(pp-listing-option); intersperse(|","); concat-strings> opts => opts-string;
            <(?[] < !"" + !",")> opts => sep
    
    pp-listing-option : ListingStyleOption(o) -> <conc-strings> ("style=", <pp-listing-option> o)
    pp-listing-option : ListingXLeftMargin(o) -> <conc-strings> ("xleftmargin=", <pp-latex-dimension> o)
    
    pp-listing-option : ListingStyleFigure() -> "figure"

    pp-latex-environment :
      LstlistingEnvironment(lname, text, rname) ->
      <conc-strings> ("\\begin{lstlisting}",
                       "[escapeinside={|>}{<|}]", 
                        text,
                      "\\end{lstlisting}")
  
    pp-latex-environment :
	    LstsugarjEnvironment(lname, opts, Locked(doc), rname) ->
	    <conc-strings> ("\\begin{lstlisting}",
	                     "[", 
	                     <pp-listing-options> opts,
	                     "language=SugarJ,escapeinside={|>}{<|}]\n", 
	                      <origin-text> doc,
	                    "\n\\end{lstlisting}")
  
  
  
  rules
    pp-latex-command :
      SugarJInlineCommandExclamation(Locked(t)) -> <conc-strings> ("\\lstinline[language=SugarJ,escapeinside={|>}{<|}]!", <origin-text> t, "!")
      
    pp-latex-command :
      SugarJInlineCommandBrace(Locked(t)) -> <conc-strings> ("\\lstinline[language=SugarJ,escapeinside={|>}{<|}]{", <origin-text> t, "}")

    pp-latex-command :
      LatexInlineCommandExclamation(Locked(t)) -> <conc-strings> ("\\lstinline[language=LaTeX,escapeinside={|>}{<|}]!", <origin-text> t, "!")
      
    pp-latex-command :
      LatexInlineCommandBrace(Locked(t)) -> <conc-strings> ("\\lstinline[language=LaTeX,escapeinside={|>}{<|}]{", <origin-text> t, "}")

    pp-latex-command :
      LstInlineCommandExclamation(s) -> <conc-strings> ("\\lstinline[language=SugarJ,escapeinside={|>}{<|}]!", s, "!")
      
    pp-latex-command :
      LstInlineCommandBrace(s) -> <conc-strings> ("\\lstinline[language=SugarJ,escapeinside={|>}{<|}]{", s, "}")
}


public editor services Listings {
  colorer
    listings = _ 255 255 150
    _.LstsugarjEnvironment : listings
    _.LstlistingEnvironment : listings
    ListingOption : black 
    ListingStyle : black
}