package text;

import text.Bibtex;

public sugar LatexLib {
  lexical syntax
    "\\maketitle" -> NullaryTextCommand
    "\\and" -> NullaryTextCommand

  
  syntax
    "\\emph" "{" LatexTextContent "}" -> InTextCommand {cons("EmphCommand")}
    "\\cite" "{" <BibtexEntries-CF> "}" -> InTextCommand {cons("CiteCommand")}
    "\\title" "{" LatexTextContent "}" -> InTextCommand {cons("TitleCommand")}
    
  
  context-free syntax
    {BibtexEntryName ","}+ -> BibtexEntries
    
  rules
    latex-environment = ?"abstract"
    latex-environment = ?"document"
  
  rules
    pp-latex = fail
    
    pp-latex-command :
      EmphCommand(content)
      ->
      <conc-strings> ("\\emph{", <pp-latex> content, "}")

    pp-latex-command :
      CiteCommand(cites)
      ->
      <conc-strings> ("\\cite{", s , "}")
      where
        <map(?BibtexEntryName(<id>))> cites => names;
        <intersperse(|","); concat-strings> names => s

    pp-latex-command :
      TitleCommand(content)
      ->
      <conc-strings> ("\\title{", <pp-latex> content, "}") 

  rules
    intersperse(|s) : [] -> []
    intersperse(|s) : [x] -> [x]
    intersperse(|s) : [x|y] -> [x,s| <intersperse(|s)> y]
  
  rules
    constraint-error :
      CiteCommand(cites)
      -> <filter(cite-error)> cites
    
    bibtex-entry = fail
    
    cite-error :
      t@BibtexEntryName(n) -> (t, <conc-strings> ("undefined citation: ", n))
      where
        <not(bibtex-entry)> n
}

public editor services LatexLib {
  colorer
    environment _.EmphCommand : 0 0 255 italic
    environment _.TitleCommand : 0 128 0 bold
  
  completions
    completion template : LatexTextEnvironmentName = "abstract"
    completion template : LatexTextEnvironmentName = "document"
  
    completion template : LatexTextContentPart = "\emph{" <text> "}"
}